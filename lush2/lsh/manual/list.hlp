$Id: list.hlp,v 1.9 2006/03/16 19:46:55 laseray Exp $

#? *** Lists

List are the elementary data structure in Lush.  Several functions
are designed for handling lists. Lists are stored as pairs (car . cdr). 
The first element of the pair (car) is a pointer to the first
element of the list. The second element of the pair (cdr) is a pointer
to the list of the remaining elements.

The textual representation of a list is composed of an opening
parenthesis, a sequence of Lush objects separated by blanks and
closing parenthesis.  The empty list is thus written as <()>.
(Note that <nil> is not another name for <()> as in some Lisp
implementations).

The second element (cdr) of a pair is not necessarily a list. A special
dotted pair notation is used for representing such a pair.  This
notation is composed of an opening parenthesis, the first Lush
objects, a blank separated dot, the second Lush object and a closing
parenthesis.

Examples:
.CODE
	(1 2 3)	        ; car=2 and cdr=(2 3)
	((q w) 2)       ; car=(q w) and cdr=2
	(1 2 (e r))     ; car=1 and cdr=(2 (e r))
	(a . b)	        ; car=a and cdr=b
.DIV

#? ** List Manipulation Functions

The following functions are useful for accessing or building lists.


#? (car <l>)	
Return first element of a list or dotted pair <l>.
The function name 'car' stands for contents of address register.

Example:
.EX (car '(a b c d))


#? (cdr <l>)	
Return rest of list <l> (or second element of dotted pair <l>).
The function name 'cdr' stands for contents of destination register.


Example:
.EX (cdr '(a b c d))


#? (c ... r <l>)	
This a variation on <car> and <cdr> to access specific, possibly nested,
elements of a list. The ellipsis (...) stands for any two or three letter
combination of the letters <a> or <d> (i.e. the address and destination). 
All the combinations of two car or cdr functions are written in C. The 
combination of three are written in Lisp in <"sysenv.lsh">.

Example:
.EX (cadr '(a b c d))


#? (cons <a1> <a2>)	
Builds a list whose car is <a1> and cdr <a2>.

Example:
.EX (cons '+ '(2 3))
.EX (cons 'a 'b)

#? (list <a1> ... <an>)	
Return a list with elements <a1> to <an>. 

.EX (list 'a '(b c) 'd)

#? (make-list <n> <v>)	
Return a list containing <n> times element <v>.

.EX (make-list 4 'e)


#? (copy-list <l>)
.SEE copy-tree
Return a new proper list with the same elements as <l>.

#? (copy-tree <tr>)
.SEE copy-list
Return a copy of <tr> in wich all conses are new but all
atoms are the same as in <tr>.

#? (circular-list <a1> ... <an>)
Return a circular list with elements <a1> to <an> repeating indefinitely.

.CODE 
   (circular-list 0 1)
   = (0 1 0 ...)
.DIV

#? (range [<n1>] <n2> [<delta>])	
Returns a list of all the numbers between <n1> and <n2>, stepping by
<delta>. The default value for both arguments <n1> and <delta> is <1>.
.SEE range*
Example:
.EX (range 2 5)


#? (range* [<n1>] <n2> [<delta>])	
Returns a list of all the numbers between <n1> and <n2>, excluding 
<n2>, stepping by <delta>. The default value for <n1> is 0, for
<delta> it is <1>.
.SEE range
Example:
.EX (range* 5)

#? (length <s>)	
Returns the length of the sequence <s>.

Example:
.EX (length '(a b c d e))
.EX (length [[1 2] [3 4]])

.P
This function is able to detect that list <l> is circular.
The value <-1> is returned when such a condition occurs.


#? (append <l1> ... <ln>)	

Concatenates lists <l1> to <ln>. 
The idiom <(append l ())> may be used to
get a fresh copy of the list <l>.

Example:
.EX (append '(1 2 3) '(4 5 6))



#? (reverse <l>)	
Returns a list of <l>'s toplevel elements in reverse order.

Example:
.EX (reverse '(1 2 3))
? (reverse '(1 2 3))
= (3 2 1)


#? (nth <n> <l>)	
Returns the <n>th elements of the list <l>. The first element is
numbered <0>.
Example:
.EX (nth 3 '(a b c d e))


#? (nthcdr <n> <l>)	
Returns the <n>th pair of the list <l>. The first pair is numbered 0.
This function is equivalent to <n> calls of the <cdr> functions.

Example:
.EX (nthcdr 3 '(a b c 3 e f))


#? (lasta <l>)	
Returns the last element ("last atom") of the list <l>.
Example:
.EX (lasta '(a b c d))
.EX (lasta '(a b c . d))


#? (last <l> [<n>])	
Return the last <n> conses of the <l> (default <n>=1).

Example:
.EX (last '(a b c))


#? (nfirst <n> <l>)

When <n> is 0, the empty list is returned.

When <n> is greater than 0, this function
returns the <n> first elements of <l>.
When the length of the list is lower than <n>,
a copy of the list is returned.

When <n> is lower than 0, this function returns
all elements but the <n> last.
When the length of the list is lower than the absolute value of <n>,
the empty list is returned.
If <l> is a circular list, an error occurs.


#? (member <e> <l>)	

Searches list <l> for element <e>. If element <e> is not found,
function <member> returns the empty list <()>. Otherwise function
<member> returns the first sublist of <l> whose first element is equal
to <e>.

There is a single equality test in Lush that is able to recursively
compare lists and strings.

Example:
.EX (member 'e (range 1 4))
.EX (member 'e '(a b c d e f g h i j))


#? (flatten <x>)	
Returns all the non nil atoms in <x>, linked in a single list.

Example:
.EX (flatten '2)
.EX (flatten '(1 2 (3 (6 7)) 4))


#? (flatten* <x>)	
Return all atoms in <x>, including <nil>s, linked in a single list.

Example:
.EX (flatten* '2)
.EX (flatten* '(1 2 (3 (6 7)) 4))


#? (filter <p> <l>)
.SEE filter*
Return list of those elements of <l> for which <p> evaluates to 
anything but <()>.

Example:
.EX (filter evenp (range 10))


#? (filter* <p> <l>)
.SEE filter
Return two lists, the first containing all elements of <l> for which <p>
evaluates to anything but <()>, the second containing all other elements.

Example:          
.EX (filter* evenp (range 10))


#? (assoc <key> <alist>)
.SEE (alist <...atoms...>)
.SEE (alist-add <key> <value> <alist>)
.SEE (alist-get <key> <alist>)
.SEE Hash Tables.

Function <assoc> is useful for searching an ``alist''.
An alist is a list of pairs <(key . value)> representing
associations between a search key <key> and a value <value>.

Function <assoc> returns the first pair of <alist> whose first element
is equal to <key>. The empty list is returned when no matching pair
is found. Once the pair is located, you can acess the value 
associated with the key using function <cdr>. You can also 
change the value using function <rplacd>.

Remark: Alists should be only used for small numbers of
key-value pairs. We suggest using hash tables as soon as
the association involves more that twelve key-value pairs.
Hash tables indeed require more memory but are much faster and
much more convenient.

#? (alist <...atoms...>)
.TYPE DE
.FILE sysenv.lsh
.SEE  (assoc <key> <alist>)
Create an alist by pairwise <cons>-ing the arguments.
.EX (alist "one" 1 "two" 2 "three" 3)

#? (copy-alist <al>)
.SEE (alist <...>)
Create a new alist containing copies of all conses in <al> and 
return it. Each cons in <al> has the same car and cdr as its
corresponding cons in the returned alist.

#? (alist-add <key> <value> <alist>)
.TYPE DE
.FILE sysenv.lsh
.SEE (assoc <key> <alist>)
Add pair (<key> . <value>) to <alist> or update <alist> with new
value and return the modified alist.
If a pair with <car> <key> already exists in <alist>, then 
<alist-add> performs an update by setting this pair's <cdr> to 
<value> (that is, when updating <alist-add> modifies the input 
<alist>).
.EX (alist-add "three" 3 (alist "one" 1 "two" 2))

#? (alist-get <key> <alist>)
.TYPE DE
.FILE sysenv.lsh
.SEE (assoc <key> <alist>)
Returns the value associated to <key> in <alist>.
.EX (alist-get "two" (alist "one" 1 "two" 2 "three" 3))

#? (alist-rm <key> <alist>)
.TYPE DE
.FILE sysenv.lsh
.SEE (assoc <key> <alist>)
Return new <alist> with pair (<key> . <whatever>) removed.
.EX (alist-rm "two" (alist "one" 1 "two" 2 "three" 3))

#? (alist-update <alist> <alist2>)
.TYPE DE
.FILE sysenv.lsh
.SEE (assoc <key> <alist>)
For each key in <alist2>, if it is present in in <alist>, update the
associated value, otherwise add the (key . value) pair in <alist2> to
<alist>.
.EX (let ((al  (alist "one" () "two" 2))
          (al2 (alist "one" 1  "three" 3)))
        (alist-update al al2) )

#? (sort-list <l> <cmp>)
.TYPE DE
.FILE sysenv.lsh
Return a sorted copy of list <l> according the order relation 
achieved by the diadic function <cmp>.
.EX (sort-list '(12 3 1 2 3 4 5 6) >)

#? (bsearch <l> <v>)
.TYPE DE
.FILE sysenv.lsh
Return the largest index of the element in <l> whose value 
is less than or equal to <v> using a binary search. <bsearch>
assumes the elements in <l> are sorted in ascending order.
.EX (let ((l '(2 4 6 8 10)))
        (nth (bsearch l 5) l) )
If <l> is a numeric vector, <bsearch> dispatches to the 
appropriate bsearch function defined in "libidx/idx-sort.lsh".


#? ** Physical List Manipulation Functions

Instead of building new pairs, a few fast functions directly change
the pointers stored in the pairs given as argument. This is safe if
the programmer knows that the function's arguments are not used
elsewhere.

Side effects should be expected when another lisp object points to 
the argument of such a physical list modification function.
The modification then appears in both places.

Physical list manipulation functions can be used to construct
lists that reference themselves. Some Lush functions 
are able to test this condition (e.g. <length>) and avoid
an infinite loop. Some Lush functions however (e.g. <print>, 
<flatten>) will loop forever when processing such a list.
You can interrupt these loops by typing <Ctrl-C> or <Break>.

.SEE (length <l>)
.SEE (== <n1> <n2>)


#? (rplaca <l> <e>)	
Physically replaces the <"car"> of list <l> by <e>.

Example:
.CODE
? (setq a '(1 2 3))
= (1 2 3)
? (setq b (cdr a))
= (2 3)
? (rplaca b 'e)    ; this is fast
= (e 3)
? a                ; but causes side effects
= (1 e 3)
.PP


#? (rplacd <l> <e>)	
Physically replaces the cdr of list <l> by <e>.


#? (displace <l1> <l2>)	
Replaces both the car and cdr of <l1> by the car and cdr of <l2>. The
main purpose of this function is the implementation of DMD functions.


#? (nconc <l1> ... <ln>)	
.TYPE DE
.FILE sysenv.lsh
Function <nconc> returns the concatenation of the 
lists <l1> to <ln>. It uses physical replacement 
and therefore causes side effects.


#? (nconc1 <l> <e>)	
.TYPE DE
.FILE sysenv.lsh
Function <nconc> returns a list formed by appending element <e>
to the end of list <l>. It uses physical replacement 
and therefore causes side effects.

Example:
.CODE
? (setq a '(a b c d e))
= (a b c d e)
? (nconc1 a 'f)
= (a b c d e f)
.PP


#? (list-insert l pos x)
.FILE sysenv.lsh
Function <list-insert> returns a list formed by inserting 
element <x> at position <pos> of list <l>. It uses physical replacement 
and therefore causes side effects.


#? (list-delete l pos)
.FILE sysenv.lsh
Function <list-delete> returns a list formed by deleting
the element located at position <pos> from list <l>. 
It uses physical replacement 
and therefore causes side effects.


#? (list-merge <l> <l2>)
.FILE sysenv.lsh
Function <list-merge> returns a list formed by 
appending to list <l> the elements of <l2> that are not
already in <l>. This function uses physical replacement 
and therefore causes side effects.


#? (list-split! <l> <n>)
.FILE sysenv.lsh
Split a list after <n> elements and return the two parts
consed.

Example:
(let (((left . right) (list-split! (range 10) 5))) left)
